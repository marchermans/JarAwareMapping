subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group 'com.ldtteam.jam'
    version System.getenv().containsKey("Version") ? System.getenv("Version") : project.buildVersion

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    dependencies {
        implementation "com.machinezoo.noexception:noexception:${noExceptionsVersion}"
        implementation "org.slf4j:slf4j-api:${slf4jVersion}"
        runtimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
        implementation "org.ow2.asm:asm:${asmVersion}"
        implementation "org.ow2.asm:asm-util:${asmVersion}"
        implementation "org.ow2.asm:asm-commons:${asmVersion}"
        implementation "com.google.guava:guava:${guavaVersion}"

        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {pub ->
                from components.java

                pom {
                    url = 'https://github.com/OrionDevelopment/JarAwareMapping'
                    licenses {
                        license {
                            name = 'GNU GENERAL PUBLIC LICENSE Version 3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'OrionDevelopment'
                            name = 'Marc Hermans'
                            email = 'marc.hermans@ldtteam.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/OrionDevelopment/JarAwareMapping.git'
                        developerConnection = 'https://github.com/OrionDevelopment/JarAwareMapping.git'
                        url = 'https://github.com/OrionDevelopment/JarAwareMapping'
                    }
                }
            }
        }

        if (System.getenv().containsKey("LDTTeamJfrogUsername") && System.getenv().containsKey("LDTTeamJfrogPassword")) {
            repositories {
                maven {
                    name 'LDTTeamJfrog'
                    credentials {
                        username System.getenv().get("LDTTeamJfrogUsername")
                        password System.getenv().get("LDTTeamJfrogPassword")
                    }
                    url 'https://ldtteam.jfrog.io/ldtteam/mods-maven'
                }
            }
        }

        if (System.getenv().containsKey("ForgeMavenUsername") && System.getenv().containsKey("ForgeMavenPassword")) {
            repositories {
                maven {
                    name 'MinecraftForge'
                    credentials {
                        username System.getenv().get("ForgeMavenUsername")
                        password System.getenv().get("ForgeMavenPassword")
                    }
                    url 'https://maven.minecraftforge.net/'
                }
            }
        }

        repositories {
            maven {
                name 'RepoDirectory'
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
